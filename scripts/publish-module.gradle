apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
//    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Pixalate Pre-Bid Fraud Blocking SDK for Android'
                    url = 'https://github.com/pixalate-inc/android-prebid-blocking'
                    licenses {
                        license {
                            name = 'GNU LPL 3.0'
                            url = 'https://github.com/pixalate-inc/android-prebid-blocking/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'pixalate'
                            name = 'Pixalate, Inc.'
                            email = 'support@pixalate.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/pixalate-inc/android-prebid-blocking.git'
                        developerConnection = 'scm:git:ssh://github.com/pixalate-inc/android-prebid-blocking.git'
                        url = 'https://github.com/pixalate-inc/android-prebid-blocking/tree/master'
                    }

                }

            }

        }

    }

}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )

    sign publishing.publications
}